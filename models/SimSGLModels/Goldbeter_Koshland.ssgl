ModelID Goldbeter_Koshland

// Define all agents (i.e. molecules) of your simulation here..
agent K(a)
agent P(a)
agent T(x{u,p}, y{u,p}) 

// Instantiate previously defined agents
init i1 100 {K(), P(), T(x{u}[free], y{u}[free])} 

// Define rules, along with a certain application rate 
rule KT_x {K(a[free]), T(x[free])} <-> {K(a[1]), T(x[1])} @ [1, 10]
rule Tp_x {K(a[1]), T(x{u}[1])} -> {K(a[1]), T(x{p}[1])} @ [1]
rule KT_y {K(a[free]), T(y[free])} <-> {K(a[1]), T(y[1])} @ [1, 10]
rule Tp_y {K(a[1]), T(y{u}[1])} -> {K(a[1]), T(y{p}[1])} @ [1]

rule PT_x {P(a[free]), T(x[free])} <-> {P(a[1]), T(x[1])} @ [1, 10]
rule Tu_x {P(a[1]), T(x{p}[1])} -> {P(a[1]), T(x{u}[1])} @ [1]
rule PT_y {P(a[free]), T(y[free])} <-> {P(a[1]), T(y[1])} @ [1, 10]
rule Tu_y {P(a[1]), T(y{p}[1])} -> {P(a[1]), T(y{u}[1])} @ [1]

// Define patterns of interest, which will be tracked an displayed at then end of the simulation
obs o1 {T(x{p}[?], y{p}[?])}
obs o2 {T(x{p}[free], y{p}[free])} 

// Termination condition
terminate t1 time=20000